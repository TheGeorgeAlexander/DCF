// uppercase is non-terminal (production rule)
// lowercase is terminal (token)

// NOTE:    All 'comment' tokens are ignored during parsing, except when parsed as part
//          of PAIR_HEADER. This implicit behavior is not reflected in the grammar below.


START           --> SECTION


SECTION         --> l_brace PAIR_LIST r_brace


PAIR_LIST       --> PAIR comma PAIR_LIST
                    | PAIR
                    | *


PAIR            --> PAIR_HEADER key colon VALUE


PAIR_HEADER     --> comment PAIR_HEADER
                    | *


VALUE           --> string
                    | boolean
                    | NUMBER
                    | ARRAY
                    | SECTION
                    | FUNCTION


NUMBER          --> num_int
                    | num_decimal
                    | num_hex
                    | num_binary


ARRAY           --> l_bracket VALUE_LIST r_bracket


VALUE_LIST      --> VALUE comma VALUE_LIST
                    | VALUE
                    | *


FUNCTION        --> function l_paren VALUE_LIST r_paren
